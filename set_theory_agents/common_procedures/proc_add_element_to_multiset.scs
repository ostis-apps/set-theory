proc_add_element_to_multiset
=> nrel_main_idtf:
	[Процедура добавления элемента в мультимножество] (* <- lang_ru;; *);
	[] (* <- lang_en;; *);
<- scp_program;
-> rrel_key_sc_element: .._process1;;

proc_add_element_to_multiset = [*
.._process1
_<- scp_process;

_-> rrel_1:: rrel_in:: _element;
_-> rrel_2:: rrel_in:: _multiset;
_-> rrel_3:: rrel_out:: _multiplicity;
_<= nrel_decomposition_of_action:: .._actions (*

	_-> rrel_1:: .._proc_add_element_to_multiset_operator_print_start (*
		_<- printNl;;
		_-> rrel_1:: rrel_scp_const:: [Начало. Процедура добавления элемента в мультимножество];;

		_=> nrel_goto:: .._proc_add_element_to_multiset_operator_check_belonging;;
	*);;

	_-> .._proc_add_element_to_multiset_operator_check_belonging (*
		_<- searchElStr3;;
		_-> rrel_1:: rrel_fixed:: rrel_scp_const:: _multiset;;
		_-> rrel_2:: rrel_assign:: rrel_pos_const_perm:: rrel_scp_var:: _belonging_arc;;
		_-> rrel_3:: rrel_fixed:: rrel_scp_const:: _element;;

		_=> nrel_else:: .._proc_add_element_to_multiset_operator_find_all_numbers;;
		_=> nrel_then:: .._proc_add_element_to_multiset_operator_print_end;;
	*);;

	_-> .._proc_add_element_to_multiset_operator_print_end (*
		_<- printNl;;
		_-> rrel_1:: rrel_fixed:: rrel_scp_const:: [Конец. Процедура добавления элемента в мультимножество];;

		_=> nrel_goto:: .._proc_add_element_to_multiset_operator_return;;
	*);;

	_-> .._proc_add_element_to_multiset_operator_find_all_numbers (*
		_<- searchSetStr3;;
		_-> rrel_1:: rrel_fixed:: rrel_scp_const:: number;;
		_-> rrel_2:: rrel_assign:: rrel_pos_const_perm:: rrel_scp_var:: _number_arc;;
		_-> rrel_3:: rrel_assign:: rrel_scp_var:: _number;;
		_-> rrel_set_3:: rrel_assign:: rrel_scp_var:: _number_set;;

		_=> nrel_else:: .._proc_add_element_to_multiset_operator_generate_number;;
		_=> nrel_then:: .._proc_add_element_to_multiset_operator_get_number;;
	*);;

	_-> .._proc_add_element_to_multiset_operator_get_number (*
		_<- searchElStr3;;
		_-> rrel_1:: rrel_fixed:: rrel_scp_var:: _number_set;;
		_-> rrel_2:: rrel_assign:: rrel_pos_const_perm:: rrel_scp_var:: _number_set_arc;;
		_-> rrel_3:: rrel_assign:: rrel_scp_var:: _number;;

		_=> nrel_else:: .._proc_add_element_to_multiset_operator_generate_number;;
		_=> nrel_then:: .._proc_add_element_to_multiset_operator_remove_number_from_set;;
	*);;

	_-> .._proc_add_element_to_multiset_operator_generate_number (*
		_<- varAssign;;
		_-> rrel_1:: rrel_assign:: rrel_scp_var:: _number_value;;
		_-> rrel_2:: rrel_fixed:: rrel_scp_var:: _multiplicity;;

		_=> nrel_goto:: .._proc_add_element_to_multiset_operator_generate_number_idtf;;
	*);;

	_-> .._proc_add_element_to_multiset_operator_generate_number_idtf (*
		_<- genElStr5;;
		_-> rrel_1:: rrel_assign:: rrel_const:: rrel_node:: rrel_scp_var:: _number;;
		_-> rrel_2:: rrel_assign:: rrel_common:: rrel_const:: rrel_scp_var:: _idtf_binary_arc;;
		_-> rrel_3:: rrel_fixed:: rrel_scp_var:: _number_value;;
		_-> rrel_4:: rrel_assign:: rrel_pos_const_perm:: rrel_scp_var:: _idtf_arc;;
		_-> rrel_5:: rrel_fixed:: rrel_scp_const:: nrel_idtf;;

		_=> nrel_goto:: .._proc_add_element_to_multiset_operator_generate_language_arc;;
	*);;

	_-> .._proc_add_element_to_multiset_operator_generate_language_arc (*
		_<- genElStr3;;
		_-> rrel_1:: rrel_fixed:: rrel_scp_const:: lang_ru;;
		_-> rrel_2:: rrel_assign:: rrel_pos_const_perm:: rrel_scp_var:: _lang_arc;;
		_-> rrel_3:: rrel_fixed:: rrel_scp_var:: _number_value;;

		_=> nrel_goto:: .._proc_add_element_to_multiset_operator_generate_number_arc;;
	*);;

	_-> .._proc_add_element_to_multiset_operator_generate_number_arc (*
		_<- genElStr3;;
		_-> rrel_1:: rrel_fixed:: rrel_scp_const:: number;;
		_-> rrel_2:: rrel_assign:: rrel_pos_const_perm:: rrel_scp_var:: _number_arc;;
		_-> rrel_3:: rrel_fixed:: rrel_scp_var:: _number;;

		_=> nrel_goto:: .._proc_add_element_to_multiset_operator_print_number_generated;;
	*);;

	_-> .._proc_add_element_to_multiset_operator_print_number_generated (*
		_<- printNl;;
		_-> rrel_1:: rrel_fixed:: rrel_scp_const:: [Нужное число сгенерировано];;

		_=> nrel_goto:: .._proc_add_element_to_multiset_operator_generate_multiplicity_class;;
	*);;

	_-> .._proc_add_element_to_multiset_operator_generate_multiplicity_class (*
		_<- genElStr5;;
		_-> rrel_1:: rrel_assign:: rrel_const:: rrel_node:: rrel_scp_var:: _multiplicity_class;;
		_-> rrel_2:: rrel_assign:: rrel_common:: rrel_const:: rrel_scp_var:: _measurement_binary_arc;;
		_-> rrel_3:: rrel_fixed:: rrel_scp_var:: _number;;
		_-> rrel_4:: rrel_assign:: rrel_pos_const_perm:: rrel_scp_var:: _measurement_arc;;
		_-> rrel_5:: rrel_fixed:: rrel_scp_const:: nrel_measurement;;

		_=> nrel_goto:: .._proc_add_element_to_multiset_operator_generate_multiplicity_arc;;
	*);;

	_-> .._proc_add_element_to_multiset_operator_generate_multiplicity_arc (*
		_<- genElStr3;;
		_-> rrel_1:: rrel_fixed:: rrel_scp_const:: multiplicity_of_belonging;;
		_-> rrel_2:: rrel_assign:: rrel_pos_const_perm:: rrel_scp_var:: _multiplicity_arc;;
		_-> rrel_3:: rrel_fixed:: rrel_scp_var:: _multiplicity_class;;

		_=> nrel_goto:: .._proc_add_element_to_multiset_operator_print_multiplicty_class_generated;;
	*);;

	_-> .._proc_add_element_to_multiset_operator_print_multiplicty_class_generated (*
		_<- printNl;;
		_-> rrel_1:: rrel_fixed:: rrel_scp_const:: [Нужный класс кратности принадлежности сгенерирован];;

		_=> nrel_goto:: .._proc_add_element_to_multiset_operator_add_element_to_multiset;;
	*);;

	_-> .._proc_add_element_to_multiset_operator_add_element_to_multiset (*
		_<- genElStr5;;
		_-> rrel_1:: rrel_fixed:: rrel_scp_const:: _multiset;;
		_-> rrel_2:: rrel_assign:: rrel_pos_const_perm:: rrel_scp_var:: _belonging_arc;;
		_-> rrel_3:: rrel_fixed:: rrel_scp_const:: _element;;
		_-> rrel_4:: rrel_assign:: rrel_pos_const_perm:: rrel_scp_var:: _multiplicity_arc;;
		_-> rrel_5:: rrel_fixed:: rrel_scp_var:: _multiplicity_class;;

		_=> nrel_goto:: .._proc_add_element_to_multiset_operator_print_end;;
	*);;

	_-> .._proc_add_element_to_multiset_operator_remove_number_from_set (*
		_<- eraseEl;;
		_-> rrel_1:: rrel_erase:: rrel_fixed:: rrel_scp_var:: _number_set_arc;;

		_=> nrel_goto:: .._proc_add_element_to_multiset_operator_get_number_value;;
	*);;

	_-> .._proc_add_element_to_multiset_operator_get_number_value (*
		_<- searchElStr5;;
		_-> rrel_1:: rrel_fixed:: rrel_scp_var:: _number;;
		_-> rrel_2:: rrel_assign:: rrel_common:: rrel_scp_var:: _idtf_binary_arc;;
		_-> rrel_3:: rrel_assign:: rrel_scp_var:: _number_value;;
		_-> rrel_4:: rrel_assign:: rrel_pos_const_perm:: rrel_scp_var:: _idtf_arc;;
		_-> rrel_5:: rrel_fixed:: rrel_scp_const:: nrel_idtf;;

		_=> nrel_else:: .._proc_add_element_to_multiset_operator_get_number;;
		_=> nrel_then:: .._proc_add_element_to_multiset_operator_compare_number_values;;
	*);;

	_-> .._proc_add_element_to_multiset_operator_compare_number_values (*
		_<- ifEq;;
		_-> rrel_1:: rrel_fixed:: rrel_scp_var:: _number_value;;
		_-> rrel_2:: rrel_fixed:: rrel_scp_var:: _multiplicity;;

		_=> nrel_else:: .._proc_add_element_to_multiset_operator_get_number;;
		_=> nrel_then:: .._proc_add_element_to_multiset_operator_print_number_found;;
	*);;

	_-> .._proc_add_element_to_multiset_operator_print_number_found (*
		_<- printNl;;
		_-> rrel_1:: rrel_fixed:: rrel_scp_const:: [Нужное число найдено];;

		_=> nrel_goto:: .._proc_add_element_to_multiset_operator_find_multiplicity_class;;
	*);;

	_-> .._proc_add_element_to_multiset_operator_find_multiplicity_class (*
		_<- searchSetStr5;;
		_-> rrel_1:: rrel_assign:: rrel_scp_var:: _multiplicity_class;;
		_-> rrel_2:: rrel_assign:: rrel_common:: rrel_scp_var:: _measurement_binary_arc;;
		_-> rrel_3:: rrel_fixed:: rrel_scp_var:: _number;;
		_-> rrel_4:: rrel_assign:: rrel_pos_const_perm:: rrel_scp_var:: _measurement_arc;;
		_-> rrel_5:: rrel_fixed:: rrel_scp_const:: nrel_measurement;;
		_-> rrel_set_1:: rrel_assign:: rrel_scp_var:: _multiplicity_set;;

		_=> nrel_else:: .._proc_add_element_to_multiset_operator_generate_multiplicity_class;;
		_=> nrel_then:: .._proc_add_element_to_multiset_operator_get_multiplicity_class;;
	*);;

	_-> .._proc_add_element_to_multiset_operator_get_multiplicity_class (*
		_<- searchElStr3;;
		_-> rrel_1:: rrel_fixed:: rrel_scp_var:: _multiplicity_set;;
		_-> rrel_2:: rrel_assign:: rrel_pos_const_perm:: rrel_scp_var:: _multiplicity_set_arc;;
		_-> rrel_3:: rrel_assign:: rrel_scp_var:: _multiplicity_class;;

		_=> nrel_else:: .._proc_add_element_to_multiset_operator_generate_multiplicity_class;;
		_=> nrel_then:: .._proc_add_element_to_multiset_operator_remove_multiplicity_from_set;;
	*);;

	_-> .._proc_add_element_to_multiset_operator_remove_multiplicity_from_set (*
		_<- eraseEl;;
		_-> rrel_1:: rrel_erase:: rrel_fixed:: rrel_scp_var:: _multiplicity_set_arc;;

		_=> nrel_goto:: .._proc_add_element_to_multiset_operator_check_multiplicity_class;;
	*);;

	_-> .._proc_add_element_to_multiset_operator_check_multiplicity_class (*
		_<- searchElStr3;;
		_-> rrel_1:: rrel_fixed:: rrel_scp_const:: multiplicity_of_belonging;;
		_-> rrel_2:: rrel_assign:: rrel_pos_const_perm:: rrel_scp_var:: _arc;;
		_-> rrel_3:: rrel_fixed:: rrel_scp_var:: _multiplicity_class;;

		_=> nrel_else:: .._proc_add_element_to_multiset_operator_get_multiplicity_class;;
		_=> nrel_then:: .._proc_add_element_to_multiset_operator_print_multiplicity_found;;
	*);;

	_-> .._proc_add_element_to_multiset_operator_print_multiplicity_found (*
		_<- printNl;;
		_-> rrel_1:: rrel_fixed:: rrel_scp_const:: [Нужный класс кратности принадлежности найден];;

		_=> nrel_goto:: .._proc_add_element_to_multiset_operator_add_element_to_multiset;;
	*);;

	_-> .._proc_add_element_to_multiset_operator_return (*
		_<- return;;

	*);;

*);;

*];;
