scp_program -> proc_of_finding_mapping_union(*
	-> rrel_params: ...(*
    	-> rrel_1: rrel_in: _map1;;
    	-> rrel_2: rrel_in: _map2;;
    	-> rrel_3: rrel_in: _answer;;
    	-> rrel_4: rrel_in: _map3;;
  	*);;
  
	-> rrel_operators: ...(*
    	-> rrel_init:  .proc_of_finding_map_union_operator1
		(*
			<- searchElStr5;;

		  	-> rrel_1: rrel_fixed: rrel_scp_var: _map1;;
		  	-> rrel_2: rrel_assign: rrel_pos_const_perm: rrel_scp_var: _arc;;
		  	-> rrel_3: rrel_assign: rrel_scp_var: _otprav1;;
		  	-> rrel_4: rrel_assign: rrel_pos_const_perm: rrel_scp_var: _arc2;;
		  	-> rrel_5: rrel_fixed: rrel_scp_const: rrel_1;;

             => nrel_goto: .proc_of_finding_map_union_operator2;;
		*);;

		-> .proc_of_finding_map_union_operator2
		(*
			<- searchSetStr3;;

		  	-> rrel_1: rrel_fixed: rrel_scp_var: _otprav1;;
		  	-> rrel_2: rrel_assign: rrel_pos_const_perm: rrel_scp_var: _arc;;
		  	-> rrel_3: rrel_assign: rrel_scp_var: _el;;

			-> rrel_set_3: rrel_assign: rrel_node: rrel_scp_var: _otprav1_set;;

             => nrel_goto: .proc_of_finding_map_union_operator3;;
		*);;

		-> .proc_of_finding_map_union_operator3 (*

			<- searchElStr5;;

		  	-> rrel_1: rrel_fixed: rrel_scp_var: _map1;;
		  	-> rrel_2: rrel_assign: rrel_pos_const_perm: rrel_scp_var: _arc;;
		  	-> rrel_3: rrel_assign: rrel_scp_var:  _prib1;;
		  	-> rrel_4: rrel_assign: rrel_pos_const_perm: rrel_scp_var: _arc2;;
		  	-> rrel_5: rrel_fixed: rrel_scp_const: rrel_2;;

            => nrel_goto: .proc_of_finding_map_union_operator4;;
		*);;

		-> .proc_of_finding_map_union_operator4 (*

			<- searchSetStr3;;

		  	-> rrel_1: rrel_fixed: rrel_scp_var: _prib1;;
		  	-> rrel_2: rrel_assign: rrel_pos_const_perm: rrel_scp_var: _arc;;
		  	-> rrel_3: rrel_assign: rrel_scp_var:  _el;;

			-> rrel_set_3: rrel_assign: rrel_node:  rrel_scp_var: _prib1_set;;

            => nrel_goto: .proc_of_finding_map_union_operator5;;
		*);;

		-> .proc_of_finding_map_union_operator5
		(*
			<- searchElStr5;;

		  	-> rrel_1: rrel_fixed: rrel_scp_var: _map2;;
		  	-> rrel_2: rrel_assign: rrel_pos_const_perm: rrel_scp_var: _arc;;
		  	-> rrel_3: rrel_assign: rrel_scp_var: _otprav2;;
		  	-> rrel_4: rrel_assign: rrel_pos_const_perm: rrel_scp_var: _arc2;;
		  	-> rrel_5: rrel_fixed: rrel_scp_const: rrel_1;;

             => nrel_goto: .proc_of_finding_map_union_operator6;;
		*);;

		-> .proc_of_finding_map_union_operator6
		(*
			<- searchSetStr3;;

		  	-> rrel_1: rrel_fixed: rrel_scp_var: _otprav2;;
		  	-> rrel_2: rrel_assign: rrel_pos_const_perm: rrel_scp_var: _arc;;
		  	-> rrel_3: rrel_assign: rrel_scp_var: _el;;

			-> rrel_set_3: rrel_assign:   rrel_node: rrel_scp_var: _otprav2_set;;

             => nrel_goto: .proc_of_finding_map_union_operator7;;
		*);;

		-> .proc_of_finding_map_union_operator7 (*

			<- searchElStr5;;

		  	-> rrel_1: rrel_fixed: rrel_scp_var: _map2;;
		  	-> rrel_2: rrel_assign: rrel_pos_const_perm: rrel_scp_var: _arc;;
		  	-> rrel_3: rrel_assign: rrel_scp_var:  _prib2;;
		  	-> rrel_4: rrel_assign: rrel_pos_const_perm: rrel_scp_var: _arc2;;
		  	-> rrel_5: rrel_fixed: rrel_scp_const: rrel_2;;

            => nrel_goto: .proc_of_finding_map_union_operator8;;
		*);;

		-> .proc_of_finding_map_union_operator8 (*

			<- searchSetStr3;;

		  	-> rrel_1: rrel_fixed: rrel_scp_var: _prib2;;
		  	-> rrel_2: rrel_assign: rrel_pos_const_perm: rrel_scp_var: _arc;;
		  	-> rrel_3: rrel_assign: rrel_scp_var:  _el;;

			-> rrel_set_3: rrel_assign: rrel_node: rrel_scp_var: _prib2_set;;

            => nrel_goto: .proc_of_finding_map_union_operator9;;
		*);;

		-> .proc_of_finding_map_union_operator9
			(*
			<- genEl;;

			-> rrel_1: rrel_assign: rrel_node: rrel_const: rrel_scp_var: _otprav_result;;
			
             => nrel_goto: .proc_of_finding_map_union_operator10;;
			*);; 


		-> .proc_of_finding_map_union_operator10
		(*
			<- call;; 

			-> rrel_1: rrel_fixed: rrel_scp_const:  proc_of_finding_union;;
			-> rrel_2: rrel_fixed: rrel_scp_const: ... (*
				-> rrel_1: rrel_fixed: rrel_scp_var: _otprav1_set;;
				-> rrel_2: rrel_fixed: rrel_scp_var: _otprav2_set;;
				-> rrel_3: rrel_fixed: rrel_scp_var: _otprav_result;;
			*);;
			-> rrel_3: rrel_assign: rrel_scp_var: _descr;;

			=>nrel_goto: .proc_of_finding_map_union_operator11;;
		*);;

 		->.proc_of_finding_map_union_operator11 
 		(*
            <- waitReturn;;

           	 -> rrel_1: rrel_fixed: rrel_scp_var: _descr;;

           	 => nrel_goto: .proc_of_finding_map_union_operator12;;
        *);;

        -> .proc_of_finding_map_union_operator12
			(*
			<- genEl;;

			-> rrel_1: rrel_assign: rrel_node: rrel_const: rrel_scp_var: _prib_result;;
			
             => nrel_goto: .proc_of_finding_map_union_operator13;;
			*);; 


		-> .proc_of_finding_map_union_operator13
		(*
			<- call;; 

			-> rrel_1: rrel_fixed: rrel_scp_const:  proc_of_finding_union;;
			-> rrel_2: rrel_fixed: rrel_scp_const: ... (*
				-> rrel_1: rrel_fixed: rrel_scp_var: _prib1_set;;
				-> rrel_2: rrel_fixed: rrel_scp_var: _prib2_set;;
				-> rrel_3: rrel_fixed: rrel_scp_var: _prib_result;;
			*);;
			-> rrel_3: rrel_assign: rrel_scp_var: _descr;;

			=>nrel_goto: .proc_of_finding_map_union_operator14;;
		*);;

 		->.proc_of_finding_map_union_operator14 
 		(*
            <- waitReturn;;

           	 -> rrel_1: rrel_fixed: rrel_scp_var: _descr;;

           	 => nrel_goto: .proc_of_finding_map_union_operator15A;;
        *);;

		

		-> .proc_of_finding_map_union_operator15A
			(*
			<- genEl;;

			-> rrel_1: rrel_assign: rrel_node: rrel_const: rrel_scp_var: _Z_result;;
			
             => nrel_goto: .proc_of_finding_map_union_operator15;;
			*);; 

		-> .proc_of_finding_map_union_operator15
		(*
			<- searchElStr5;;

		  	-> rrel_1: rrel_fixed: rrel_scp_var: _map1;;
		  	-> rrel_2: rrel_assign: rrel_pos_const_perm: rrel_scp_var: _arc;;
		  	-> rrel_3: rrel_assign: rrel_scp_var: _nrel_Z1;;
		  	-> rrel_4: rrel_assign: rrel_pos_const_perm: rrel_scp_var: _arc2;;
		  	-> rrel_5: rrel_fixed: rrel_scp_const: rrel_3;;

             => nrel_goto: .proc_of_finding_map_union_operator16;;
		*);;

	
		-> .proc_of_finding_map_union_operator16
		(*
			<- searchElStr5;;

		  	-> rrel_1: rrel_fixed: rrel_scp_var: _map2;;
		  	-> rrel_2: rrel_assign: rrel_pos_const_perm: rrel_scp_var: _arc;;
		  	-> rrel_3: rrel_assign: rrel_scp_var: _nrel_Z2;;
		  	-> rrel_4: rrel_assign: rrel_pos_const_perm: rrel_scp_var: _arc2;;
		  	-> rrel_5: rrel_fixed: rrel_scp_const: rrel_3;;

             => nrel_goto: .proc_of_finding_map_union_operator20A;;
		*);;
		//============================================================================
		//============================================================================
		//============================================================================
        //находим пересечение дуг
        -> .proc_of_finding_map_union_operator20A
		(*
			<- searchSetStr3;;

		  	-> rrel_1: rrel_fixed: rrel_scp_var: _otprav_result;;
		  	-> rrel_2: rrel_assign: rrel_pos_const_perm: rrel_scp_var: _arc;;
		  	-> rrel_3: rrel_assign: rrel_scp_var: _els;;

		  	->rrel_set_3: rrel_assign: rrel_node: rrel_scp_var: _otprav_els;;

             => nrel_goto: .proc_of_finding_map_union_operator20B1;;
		*);;

		-> .proc_of_finding_map_union_operator20B1
		(*
			<- ifVarAssign;;

		  	-> rrel_1: rrel_scp_var: _otprav_els;;

             => nrel_then: .proc_of_finding_map_union_operator20B;;
             => nrel_else: .proc_of_finding_map_union_operatorS1;;
		*);;

		-> .proc_of_finding_map_union_operator20B
		(*
			<- searchElStr3;;
			-> rrel_1: rrel_fixed: rrel_scp_var: _otprav_els;;
			-> rrel_2: rrel_assign: rrel_pos_const_perm: rrel_scp_var: _arc1;;
			-> rrel_3: rrel_assign: rrel_scp_var: _temp_el;;

             => nrel_then: .proc_of_finding_map_union_operator20C;;
             => nrel_else: .proc_of_finding_map_union_operatorS1;; //завершение цикла
		*);;

		-> .proc_of_finding_map_union_operator20C
		(*
			<- eraseEl;;
		 	-> rrel_1: rrel_fixed: rrel_erase: rrel_scp_var: _arc1;;

             => nrel_goto: .proc_of_finding_map_union_operator20D;;
		*);;

		-> .proc_of_finding_map_union_operator20D
		(*
			<- searchElStr5;;
			-> rrel_1: rrel_fixed: rrel_scp_var: _temp_el;;
			-> rrel_2: rrel_assign: rrel_common: rrel_scp_var: _arc_common;;
			-> rrel_3: rrel_assign: rrel_scp_var: _prib_el;;			
			-> rrel_4: rrel_assign: rrel_pos_const_perm: rrel_scp_var: _arc1;;
			-> rrel_5: rrel_fixed: rrel_scp_var: _nrel_Z1;;

             => nrel_then: .proc_of_finding_map_union_operator20F;;
             => nrel_else: .proc_of_finding_map_union_operator20B;; // след. итерация
		*);;
		-> .proc_of_finding_map_union_operator20F
		(*
			<- searchSetStr5;;

			-> rrel_1: rrel_fixed: rrel_scp_var: _temp_el;;
			-> rrel_2: rrel_assign: rrel_common: rrel_scp_var: _arc_common;;
			-> rrel_3: rrel_fixed: rrel_scp_var: _prib_el;;			
			-> rrel_4: rrel_assign: rrel_pos_const_perm: rrel_scp_var: _arc1;;
			-> rrel_5: rrel_fixed: rrel_scp_var: _nrel_Z1;; //было assign

			->rrel_set_2: rrel_fixed: rrel_scp_var: _Z_result;;

             => nrel_goto: .proc_of_finding_map_union_operator20B;;
		*);;

        //---------------------------------------------------------------

		 -> .proc_of_finding_map_union_operatorS1
		(*
			<- searchSetStr3;;

		  	-> rrel_1: rrel_fixed: rrel_scp_var: _otprav_result;;
		  	-> rrel_2: rrel_assign: rrel_pos_const_perm: rrel_scp_var: _arc;;
		  	-> rrel_3: rrel_assign: rrel_scp_var: _els;;

		  	->rrel_set_3: rrel_assign: rrel_node: rrel_scp_var: _otprav_els;;

             => nrel_goto: .proc_of_finding_map_union_operatorS2;;
		*);;

		-> .proc_of_finding_map_union_operatorS2
		(*
			<- ifVarAssign;;

		  	-> rrel_1: rrel_scp_var: _otprav_els;;

             => nrel_then: .proc_of_finding_map_union_operatorS3;;
             => nrel_else: .proc_of_finding_map_union_operator23;;
		*);;

		-> .proc_of_finding_map_union_operatorS3
		(*
			<- searchElStr3;;
			-> rrel_1: rrel_fixed: rrel_scp_var: _otprav_els;;
			-> rrel_2: rrel_assign: rrel_pos_const_perm: rrel_scp_var: _arc1;;
			-> rrel_3: rrel_assign: rrel_scp_var: _temp_el;;

             => nrel_then: .proc_of_finding_map_union_operatorS4;;
             => nrel_else: .proc_of_finding_map_union_operator23;; //завершение цикла
		*);;

		-> .proc_of_finding_map_union_operatorS4
		(*
			<- eraseEl;;
		 	-> rrel_1: rrel_fixed: rrel_erase: rrel_scp_var: _arc1;;

             => nrel_goto: .proc_of_finding_map_union_operatorS5;;
		*);;

		-> .proc_of_finding_map_union_operatorS5
		(*
			<- searchElStr5;;
			-> rrel_1: rrel_fixed: rrel_scp_var: _temp_el;;
			-> rrel_2: rrel_assign: rrel_common: rrel_scp_var: _arc_common;;
			-> rrel_3: rrel_assign: rrel_scp_var: _prib_el;;			
			-> rrel_4: rrel_assign: rrel_pos_const_perm: rrel_scp_var: _arc1;;
			-> rrel_5: rrel_fixed: rrel_scp_var: _nrel_Z2;;

             => nrel_then: .proc_of_finding_map_union_operatorS6;;
             => nrel_else: .proc_of_finding_map_union_operatorS3;; // след. итерация
		*);; 
		
		//проверяем, есть ли найденная дуга в первом соответствии
		//если есть, переходим к следующей итерации 
		//если нет, добавляем ее в результирующее множество дуг Z_result
		-> .proc_of_finding_map_union_operatorS6
		(*
			<- searchElStr5;;
			-> rrel_1: rrel_fixed: rrel_scp_var: _temp_el;;
			-> rrel_2: rrel_assign: rrel_common: rrel_scp_var: _arc_common;;
			-> rrel_3: rrel_fixed: rrel_scp_var: _prib_el;;			
			-> rrel_4: rrel_assign: rrel_pos_const_perm: rrel_scp_var: _arc1;;
			-> rrel_5: rrel_fixed: rrel_scp_var: _nrel_Z1;;

             => nrel_then: .proc_of_finding_map_union_operatorS3;; // след. итерация
             => nrel_else: .proc_of_finding_map_union_operatorS61;; 
		*);; 

		-> .proc_of_finding_map_union_operatorS61
		(*
			<- searchSetStr5;;

			-> rrel_1: rrel_fixed: rrel_scp_var: _temp_el;;
			-> rrel_2: rrel_assign: rrel_common: rrel_scp_var: _arc_common;;
			-> rrel_3: rrel_fixed: rrel_scp_var: _prib_el;;			
			-> rrel_4: rrel_assign: rrel_pos_const_perm: rrel_scp_var: _arc1;;
			-> rrel_5: rrel_fixed: rrel_scp_var: _nrel_Z2;; //было assign

			->rrel_set_2: rrel_fixed: rrel_scp_var: _Z_result;;

             => nrel_goto: .proc_of_finding_map_union_operatorS3;;
		*);;
       //============================================================================
       //============================================================================
       //============================================================================
		-> .proc_of_finding_map_union_operator23
		(*
			<- genElStr5;;

		  	-> rrel_1: rrel_fixed: rrel_scp_var: _map3;;
		  	-> rrel_2: rrel_assign: rrel_pos_const_perm: rrel_scp_var: _arc;;
		  	-> rrel_3: rrel_fixed: rrel_scp_var: _otprav_result;;
		  	-> rrel_4: rrel_assign: rrel_pos_const_perm: rrel_scp_var: _arc2;;
		  	-> rrel_5: rrel_fixed: rrel_scp_const: rrel_1;;

             => nrel_goto: .proc_of_finding_map_union_operator24;;
		*);;

		-> .proc_of_finding_map_union_operator24
		(*
			<- genElStr5;;

		  	-> rrel_1: rrel_fixed: rrel_scp_var: _map3;;
		  	-> rrel_2: rrel_assign: rrel_pos_const_perm: rrel_scp_var: _arc;;
		  	-> rrel_3: rrel_fixed: rrel_scp_var: _prib_result;;
		  	-> rrel_4: rrel_assign: rrel_pos_const_perm: rrel_scp_var: _arc2;;
		  	-> rrel_5: rrel_fixed: rrel_scp_const: rrel_2;;

             => nrel_goto: .proc_of_finding_map_union_operator25;;
		*);;

		-> .proc_of_finding_map_union_operator25
		(*
			<- genElStr5;;

		  	-> rrel_1: rrel_fixed: rrel_scp_var: _map3;;
		  	-> rrel_2: rrel_assign: rrel_pos_const_perm: rrel_scp_var: _arc;;
		  	-> rrel_3: rrel_fixed: rrel_scp_var: _Z_result;;
		  	-> rrel_4: rrel_assign: rrel_pos_const_perm: rrel_scp_var: _arc2;;
		  	-> rrel_5: rrel_fixed: rrel_scp_const: rrel_3;;

             => nrel_goto: .proc_of_finding_map_union_operator26;;
		*);;


//------------------------------------------
//добаляем все эл-ты в ответ

-> .proc_of_finding_map_union_operator26 (*
            <- call;;
            -> rrel_1: rrel_fixed: rrel_scp_const: proc_add_mapping;;
            -> rrel_2: ... (*
                -> rrel_1: rrel_fixed: rrel_scp_var: _map3;;
		-> rrel_2: rrel_fixed: rrel_scp_var: _answer;;
            *);;
            -> rrel_3: rrel_assign: rrel_scp_var: _argument;;
            => nrel_goto: .proc_of_finding_map_union_operator27;; 
        *);;

        ->.proc_of_finding_map_union_operator27 (*
            <- waitReturn;;
            -> rrel_1: rrel_fixed: rrel_scp_var: _argument;;
            => nrel_goto: .proc_of_finding_map_union_operator_return;; 
        *);;
	    -> .proc_of_finding_map_union_operator_return
	    (*
    	  <- return;;
    *);;
  *);;
*);;
