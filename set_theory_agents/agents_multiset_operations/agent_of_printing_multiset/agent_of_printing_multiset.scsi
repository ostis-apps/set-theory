agent_of_printing_multiset
=> nrel_main_idtf:
	[агентная scp-программа нахождения элементов мультимножества] 
		(* <- lang_ru;; *);
	[agent scp-program of finding multiset elements]
		(* <- lang_en;; *);
<- agent_scp_program;;

scp_program -> agent_of_printing_multiset
(*

	-> rrel_params: .agent_of_printing_multiset_operator_params
	(*
		-> rrel_1: rrel_in: _event;;
		-> rrel_2: rrel_in: _input_arc;;
	*);;

	-> rrel_operators: .agent_of_printing_multiset_operators_set
	(*
		-> rrel_init: .agent_of_printing_multiset_operator1
		(*
			<- searchElStr3;;

			-> rrel_1: rrel_assign: rrel_scp_var: _temp;;
			-> rrel_2: rrel_fixed: rrel_scp_var: _input_arc;;
			-> rrel_3: rrel_assign: rrel_scp_var: _quest;;

			=> nrel_goto: .agent_of_printing_multiset_operator2;;
		*);;

		-> .agent_of_printing_multiset_operator2
		(*
			<- searchElStr3;;

			-> rrel_1: rrel_fixed: rrel_scp_const: question_printing_multiset;;
			-> rrel_2: rrel_assign: rrel_pos_const_perm: rrel_scp_var: _arc;;
			-> rrel_3: rrel_fixed: rrel_scp_var: _quest;;

			=> nrel_then: .agent_of_printing_multiset_operator3;;
			=> nrel_else: .agent_of_printing_multiset_operator_return;;
		*);;

		-> .agent_of_printing_multiset_operator3
		(*
			<- printNl;;

			-> rrel_1: rrel_scp_const: [Агент нахождения элементов мультимножества];;

			=> nrel_goto: .agent_of_printing_multiset_operator4;;
   		*);;

		-> .agent_of_printing_multiset_operator4
		(*
			<- searchElStr5;;

			-> rrel_1: rrel_fixed: rrel_scp_var: _quest;;
			-> rrel_2: rrel_assign: rrel_pos_const_perm: rrel_scp_var: _arc_to_multiset;;
			-> rrel_3: rrel_assign: rrel_scp_var: _multiset;;
			-> rrel_4: rrel_assign: rrel_pos_const_perm: rrel_scp_var: _arc_rrel;;
			-> rrel_5: rrel_fixed: rrel_scp_const: rrel_1;;

			=> nrel_then: .agent_of_printing_multiset_operator5;;
			=> nrel_else: .agent_of_printing_multiset_operator_return;;
		*);;

		-> .agent_of_printing_multiset_operator5
		(*
			<- call;;

			-> rrel_1: rrel_fixed: rrel_scp_const: proc_add_multiset_to_answer;;
			-> rrel_2: rrel_fixed: rrel_scp_const: ...
			(*
					-> rrel_1: rrel_fixed: rrel_scp_var: _multiset;;
					-> rrel_2: rrel_assign: rrel_scp_var: _answer;;
			*);;
			-> rrel_3: rrel_assign: rrel_scp_var: _proc_descriptor;;

			=> nrel_goto: .agent_of_printing_multiset_operator6;;
		*);;

		-> .agent_of_printing_multiset_operator6
		(*
			<- waitReturn;;

			-> rrel_1: rrel_fixed: rrel_scp_var: _proc_descriptor;;

			=> nrel_goto: .agent_of_printing_multiset_operator7;;
		*);;

		-> .agent_of_printing_multiset_operator7
		(*
			<- genElStr5;;

			-> rrel_1: rrel_fixed: rrel_scp_var: _quest;;
			-> rrel_2: rrel_assign: rrel_const: rrel_common: rrel_const: rrel_scp_var: _arc;;
			-> rrel_3: rrel_fixed: rrel_scp_var: _answer;;
			-> rrel_4: rrel_assign: rrel_pos_const_perm: rrel_scp_var: _arc2;;
			-> rrel_5: rrel_fixed: rrel_scp_const: nrel_answer;;
			
			=> nrel_goto: .agent_of_printing_multiset_operator_return;;
		*);;
	
		-> .agent_of_printing_multiset_operator_return
		(*
			<- return;;
		*);;
	*);;
*);;
