proc_add_multiset_peak_value_to_answer
=> nrel_main_idtf:
	[] (* <- lang_ru;; *);
	[] (* <- lang_en;; *);
<- scp_program;
-> rrel_key_sc_element: .._process1;;

proc_add_multiset_peak_value_to_answer = [*
.._process1
_<- scp_process;

_-> rrel_1:: rrel_in:: _multiset;
_-> rrel_2:: rrel_in:: _peak_value;
_-> rrel_3:: rrel_out:: _answer;
_<= nrel_decomposition_of_action:: .._actions (*

	_-> rrel_1:: .._proc_add_multiset_peak_value_to_answer_operator_print_start (*
		_<- printNl;;
		_-> rrel_1:: rrel_scp_const:: [Начало. Процедура добавления высоты мультимножества в ответ];;

		_=> nrel_goto:: .._proc_add_multiset_peak_value_to_answer_operator_generate_answer;;
	*);;

	_-> .._proc_add_multiset_peak_value_to_answer_operator_generate_answer (*
		_<- genEl;;
		_-> rrel_1:: rrel_assign:: rrel_const:: rrel_node:: rrel_scp_var:: _answer;;

		_=> nrel_goto:: .._proc_add_multiset_peak_value_to_answer_operator_generate_peak_number;;
	*);;

	_-> .._proc_add_multiset_peak_value_to_answer_operator_generate_peak_number (*
		_<- genEl;;
		_-> rrel_1:: rrel_assign:: rrel_const:: rrel_node:: rrel_scp_var:: _peak_value_number;;

		_=> nrel_goto:: .._proc_add_multiset_peak_value_to_answer_generate_relation;;
	*);;

	_-> .._proc_add_multiset_peak_value_to_answer_generate_relation (*
		_<- genElStr5;;
		_-> rrel_1:: rrel_fixed:: rrel_scp_const:: _multiset;;
		_-> rrel_2:: rrel_assign:: rrel_common:: rrel_const:: rrel_scp_var:: _binary_arc;;
		_-> rrel_3:: rrel_fixed:: rrel_scp_var:: _peak_value_number;;
		_-> rrel_4:: rrel_assign:: rrel_pos_const_perm:: rrel_scp_var:: _arc;;
		_-> rrel_5:: rrel_fixed:: rrel_scp_const:: nrel_the_peak_value_of_the_multiset;;

		_=> nrel_goto:: .._proc_add_multiset_peak_value_to_answer_generate_number_connection;;
	*);;

	_-> .._proc_add_multiset_peak_value_to_answer_generate_number_connection (*
		_<- genElStr3;;
		_-> rrel_1:: rrel_fixed:: rrel_scp_const:: number;;
		_-> rrel_2:: rrel_assign:: rrel_pos_const_perm:: rrel_scp_var:: _arc;;
		_-> rrel_3:: rrel_fixed:: rrel_scp_var:: _peak_value_number;;

		_=> nrel_goto:: .._proc_add_multiset_peak_value_to_answer_generate_number_idtf;;
	*);;

	_-> .._proc_add_multiset_peak_value_to_answer_generate_number_idtf (*
		_<- genElStr5;;
		_-> rrel_1:: rrel_fixed:: rrel_scp_var:: _peak_value_number;;
		_-> rrel_2:: rrel_assign:: rrel_common:: rrel_const:: rrel_scp_var:: _binary_arc;;
		_-> rrel_3:: rrel_fixed:: rrel_scp_const:: _peak_value;;
		_-> rrel_4:: rrel_assign:: rrel_pos_const_perm:: rrel_scp_var:: _arc;;
		_-> rrel_5:: rrel_fixed:: rrel_scp_const:: nrel_idtf;;

		_=> nrel_goto:: .._proc_add_multiset_peak_value_to_answer_generate_lang_link;;
	*);;

	_-> .._proc_add_multiset_peak_value_to_answer_generate_lang_link (*
		_<- genElStr3;;
		_-> rrel_1:: rrel_fixed:: rrel_scp_const:: lang_ru;;
		_-> rrel_2:: rrel_assign:: rrel_pos_const_perm:: rrel_scp_var:: _arc;;
		_-> rrel_3:: rrel_fixed:: rrel_scp_const:: _peak_value;;

		_=> nrel_goto:: .._proc_add_multiset_peak_value_to_answer_operator_form_answer;;
	*);;

	_-> .._proc_add_multiset_peak_value_to_answer_operator_form_answer (*
		_<- genElStr3;;
		_-> rrel_1:: rrel_fixed:: rrel_scp_var:: _answer;;
		_-> rrel_2:: rrel_assign:: rrel_pos_const_perm:: rrel_scp_var:: _arc;;
		_-> rrel_3:: rrel_fixed:: rrel_scp_const:: _multiset;;

		_=> nrel_goto:: .._proc_add_multiset_peak_value_to_answer_operator_add_relation;;
	*);;

	_-> .._proc_add_multiset_peak_value_to_answer_operator_add_relation (*
		_<- searchSetStr5;;
		_-> rrel_1:: rrel_fixed:: rrel_scp_const:: _multiset;;
		_-> rrel_2:: rrel_assign:: rrel_common:: rrel_const:: rrel_scp_var:: _binary_arc;;
		_-> rrel_3:: rrel_assign:: rrel_scp_var:: _peak_value_number;;
		_-> rrel_4:: rrel_assign:: rrel_pos_const_perm:: rrel_scp_var:: _arc;;
		_-> rrel_5:: rrel_fixed:: rrel_scp_const:: nrel_the_peak_value_of_the_multiset;;
		_-> rrel_set_2:: rrel_fixed:: rrel_scp_var:: _answer;;
		_-> rrel_set_3:: rrel_fixed:: rrel_scp_var:: _answer;;
		_-> rrel_set_4:: rrel_fixed:: rrel_scp_var:: _answer;;

		_=> nrel_then:: .._proc_add_multiset_peak_value_to_answer_operator_add_number;;
	*);;

	_-> .._proc_add_multiset_peak_value_to_answer_operator_add_number (*
		_<- searchSetStr3;;
		_-> rrel_1:: rrel_fixed:: rrel_scp_const:: number;;
		_-> rrel_2:: rrel_assign:: rrel_pos_const_perm:: rrel_scp_var:: _arc;;
		_-> rrel_3:: rrel_fixed:: rrel_scp_var:: _peak_value_number;;
		_-> rrel_set_2:: rrel_fixed:: rrel_scp_var:: _answer;;

		_=> nrel_goto:: .._proc_add_multiset_peak_value_to_answer_add_number_idtf;;
	*);;

	_-> .._proc_add_multiset_peak_value_to_answer_add_number_idtf (*
		_<- searchSetStr5;;
		_-> rrel_1:: rrel_fixed:: rrel_scp_var:: _peak_value_number;;
		_-> rrel_2:: rrel_assign:: rrel_common:: rrel_const:: rrel_scp_var:: _binary_arc;;
		_-> rrel_3:: rrel_assign:: rrel_scp_var:: _peak_value_link;;
		_-> rrel_4:: rrel_assign:: rrel_pos_const_perm:: rrel_scp_var:: _idtf_arc;;
		_-> rrel_5:: rrel_fixed:: rrel_scp_const:: nrel_idtf;;
		_-> rrel_set_2:: rrel_fixed:: rrel_scp_var:: _answer;;
		_-> rrel_set_3:: rrel_fixed:: rrel_scp_var:: _answer;;
		_-> rrel_set_4:: rrel_fixed:: rrel_scp_var:: _answer;;

		_=> nrel_goto:: .._proc_add_multiset_peak_value_to_answer_add_lang_link;;
	*);;

	_-> .._proc_add_multiset_peak_value_to_answer_add_lang_link (*
		_<- searchSetStr3;;
		_-> rrel_1:: rrel_fixed:: rrel_scp_const:: lang_ru;;
		_-> rrel_2:: rrel_assign:: rrel_pos_const_perm:: rrel_scp_var:: _arc;;
		_-> rrel_3:: rrel_fixed:: rrel_scp_const:: _peak_value;;
		_-> rrel_set_2:: rrel_fixed:: rrel_scp_var:: _answer;;

		_=> nrel_goto:: .._proc_add_multiset_peak_value_to_answer_operator_add_nrel_peak_value;;
	*);;

	_-> .._proc_add_multiset_peak_value_to_answer_operator_add_nrel_peak_value (*
		_<- genElStr3;;
		_-> rrel_1:: rrel_fixed:: rrel_scp_var:: _answer;;
		_-> rrel_2:: rrel_assign:: rrel_pos_const_perm:: rrel_scp_var:: _arc_from_answer;;
		_-> rrel_3:: rrel_fixed:: rrel_scp_const:: nrel_the_peak_value_of_the_multiset;;

		_=> nrel_goto:: .._proc_add_multiset_peak_value_to_answer_operator_add_lang_to_answer;;
	*);;

	_-> .._proc_add_multiset_peak_value_to_answer_operator_add_lang_to_answer (*
		_<- genElStr3;;
		_-> rrel_1:: rrel_fixed:: rrel_scp_var:: _answer;;
		_-> rrel_2:: rrel_assign:: rrel_pos_const_perm:: rrel_scp_var:: _arc_from_answer;;
		_-> rrel_3:: rrel_fixed:: rrel_scp_const:: lang_ru;;

		_=> nrel_goto:: .._proc_add_multiset_peak_value_to_answer_operator_add_number_to_answer;;
	*);;

	_-> .._proc_add_multiset_peak_value_to_answer_operator_add_number_to_answer (*
		_<- genElStr3;;
		_-> rrel_1:: rrel_fixed:: rrel_scp_var:: _answer;;
		_-> rrel_2:: rrel_assign:: rrel_pos_const_perm:: rrel_scp_var:: _arc_from_answer;;
		_-> rrel_3:: rrel_fixed:: rrel_scp_const:: number;;

		_=> nrel_goto:: .._proc_add_multiset_peak_value_to_answer_operator_add_idtf_to_answer;;
	*);;

	_-> .._proc_add_multiset_peak_value_to_answer_operator_add_idtf_to_answer (*
		_<- genElStr3;;
		_-> rrel_1:: rrel_fixed:: rrel_scp_var:: _answer;;
		_-> rrel_2:: rrel_assign:: rrel_pos_const_perm:: rrel_scp_var:: _arc_from_answer;;
		_-> rrel_3:: rrel_fixed:: rrel_scp_const:: nrel_idtf;;

		_=> nrel_goto:: .._proc_add_multiset_peak_value_to_answer_operator_print_end;;
	*);;

	_-> .._proc_add_multiset_peak_value_to_answer_operator_print_end (*
		_<- printNl;;
		_-> rrel_1:: rrel_scp_const:: [Конец. Процедура добавления пикового значения мультимножества в ответ];;

		_=> nrel_goto:: .._proc_add_multiset_peak_value_to_answer_operator_return;;
	*);;

	_-> .._proc_add_multiset_peak_value_to_answer_operator_return (*
		_<- return;;

	*);;

*);;

*];;
