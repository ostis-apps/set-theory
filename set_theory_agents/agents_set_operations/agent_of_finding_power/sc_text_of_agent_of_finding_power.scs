sc_text_of_agent_of_finding_buildings_power
-> rrel_key_sc_element:
	agent_of_finding_power;

<- scp_program_sc_text;

=> nrel_main_idtf: 
	[sc-текст агентной scp-программы нахождения мощности множества] 
	(* <- lang_ru;; *);
	[sc-text scp-agent based program finding the power of sets] 
	(* <- lang_en;; *);;

sc_text_of_agent_of_finding_buildings_power = [*
agent_of_finding_power
=> nrel_main_idtf:
	[агентная scp-программа нахождения мощности множества] 
	(* <- lang_ru;; *);
	[agent scp-program of finding power] 
	(* <- lang_en;; *);
<- agent_scp_program;;

scp_program -> agent_of_finding_power 
	(*
		-> rrel_params: ..agent_of_finding_operator_power_params 
			(*
				-> rrel_1: rrel_in: _event;;
				-> rrel_2: rrel_in: _input_arc;;
			*);;
		-> rrel_operators: ..agent_of_finding_operator_power_set 
			(*
		-> rrel_init: _agent_of_finding_operator_power1 
			(*
				<- searchElStr3;;

				-> rrel_1: rrel_assign: rrel_scp_var: _temp;;
				-> rrel_2: rrel_fixed: rrel_scp_var: _input_arc;;
				-> rrel_3: rrel_assign: rrel_scp_var: _quest;;

				=>nrel_goto: _agent_of_finding_operator_power2;;
			*);;
		-> _agent_of_finding_operator_power2 
			(*
				<- searchElStr3;;

				-> rrel_1: rrel_fixed: rrel_scp_const: question_finding_power;;
				-> rrel_2: rrel_assign: rrel_pos_const_perm: rrel_scp_var: _arc;;
				-> rrel_3: rrel_fixed: rrel_scp_var: _quest;;

				=>nrel_then: _agent_of_finding_operator_power3;;
				=>nrel_else: _agent_of_finding_operator_power3/*_agent_of_finding_operator_power_return*/;;
			*);;
		-> _agent_of_finding_operator_power3 
			(*
				<- printNl;;

				-> rrel_1: rrel_fixed: rrel_scp_const: [агент нахождения мощности множества];;

				=> nrel_goto: _agent_of_finding_operator_power4;;
	   		*);;
   		-> _agent_of_finding_operator_power4 
			(*
				<- searchElStr3;;

				-> rrel_1: rrel_fixed: rrel_scp_var: _quest;;
				-> rrel_2: rrel_assign: rrel_pos_const_perm: rrel_scp_var: _arc;;
				-> rrel_3: rrel_assign: rrel_scp_var: _set1;;

				=>nrel_then: _agent_of_finding_operator_power4A;;
				=>nrel_else: _agent_of_finding_operator_power_return;;
			*);;
		-> _agent_of_finding_operator_power4A
			(*
				<- contAssign;;

				-> rrel_1: rrel_assign: rrel_scp_var: _current;;
				-> rrel_2: rrel_fixed: rrel_scp_const: [0];;
					 	
				=> nrel_goto: _agent_of_finding_operator_power5;;
			*);;		
		-> _agent_of_finding_operator_power5 
			(*
				<- call;;

			   	-> rrel_1: rrel_fixed: rrel_scp_const: proc_of_finding_power;;
			    	-> rrel_2: ... 
					(*
						-> rrel_1: rrel_fixed: rrel_scp_var: _set1;;
						-> rrel_2: rrel_fixed: rrel_scp_var: _current;;
				 	*);;
			    	-> rrel_3: rrel_assign: rrel_scp_var: _argument;;

			    	=> nrel_goto: _agent_of_finding_operator_power6;;
			*);;
        	->_agent_of_finding_operator_power6 
			(*
			    	<- waitReturn;;

			    	-> rrel_1: rrel_fixed: rrel_scp_var: _argument;;

			    	=> nrel_goto: _agent_of_finding_operator_power7;;
			*);;
		-> _agent_of_finding_operator_power7 
			(*
			   	<- varAssign;;

				-> rrel_1: rrel_assign: rrel_scp_var: _var_nrel;;
				-> rrel_2: rrel_fixed: rrel_scp_const: nrel_power_set;;

				=> nrel_goto: _agent_of_finding_operator_power8;;
			*);;
        	-> _agent_of_finding_operator_power8 
			(*
			    	<- call;;

			    	-> rrel_1: rrel_fixed: rrel_scp_const: proc_add_decimal_numeral_system;;
			    	-> rrel_2: ... 
					(*
						-> rrel_1: rrel_fixed: rrel_scp_var: _set1;;
						-> rrel_2: rrel_fixed: rrel_scp_var: _var_nrel;;
						-> rrel_3: rrel_fixed: rrel_scp_var: _current;;
						-> rrel_4: rrel_assign: rrel_scp_var: _answer;;
					*);;
            			-> rrel_3: rrel_assign: rrel_scp_var: _argumentq;;

            			=> nrel_goto: _agent_of_finding_operator_power9;;
        		*);;
        	->_agent_of_finding_operator_power9 
			(*
			        <- waitReturn;;

		            	-> rrel_1: rrel_fixed: rrel_scp_var: _argumentq;;

            			=> nrel_goto: _agent_of_finding_operator_power10;;
        		*);;
		-> _agent_of_finding_operator_power10
			(*
				<- genElStr5;;

				-> rrel_1: rrel_fixed: rrel_scp_var: _quest;;
				-> rrel_2: rrel_assign: rrel_const: rrel_common: rrel_const: rrel_scp_var: _arc;;
				-> rrel_3: rrel_fixed: rrel_scp_var: _answer;;
				-> rrel_4: rrel_assign: rrel_pos_const_perm: rrel_scp_var: _arc2;;
				-> rrel_5: rrel_fixed: rrel_scp_const: nrel_answer;;

				=>nrel_goto: _agent_of_finding_operator_power_return;;
			*);;	
		-> _agent_of_finding_operator_power_return 
			(*
				<- return;;
			*);;
		*);;
	*);;
*];;